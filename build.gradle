plugins {
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
}

group = 'com.github.karar-rifat'
archivesBaseName = "communicationLibrary"
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

// Signing
apply plugin: 'signing'
signing {
	sign configurations.archives
}

// Deploying
apply plugin: 'maven'
task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}
task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}
artifacts {
	archives javadocJar, sourcesJar
}

uploadArchives {
	repositories {
		mavenDeployer {

			// Sign POM
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			// Destination
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			// Add required metadata to POM
			pom.project {
				name 'communicationLibrary'
				packaging 'jar'
				description 'A library to send message via activemq.'
				url 'https://github.com/karar-rifat/communicationLibrary'

				scm {
					connection 'scm:git:git://github.com/karar-rifat/communicationLibrary.git'
					developerConnection 'scm:git:ssh://github.com/karar-rifat/communicationLibrary.git'
					url 'http://github.com/karar-rifat/communicationLibrary/tree/master'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'karar'
						name 'karar rifat'
						email 'kararrifat@gmail.com'
						organization 'wib'
						organizationUrl ''
					}
				}
			}
		}
	}
}

